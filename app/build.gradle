def apiKeyPropertiesFile = rootProject.file("apiKey.properties")
def apiKeyProperties = new Properties()
apiKeyProperties.load(new FileInputStream(apiKeyPropertiesFile))

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:1.0.3'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: "androidx.navigation.safeargs"
apply plugin: 'com.getkeepsafe.dexcount'

repositories {

    mavenCentral()
}

android {
    signingConfigs {
        config {
            keyAlias KEY_ALIAS
            keyPassword KEY_PASSWORD
            storeFile file('../-yusuf.keystore')
            storePassword STORE_PASSWORD
        }
    }
    compileSdkVersion 29
    buildToolsVersion "29.0.3"
    flavorDimensions "default"
    defaultConfig {
        applicationId "com.yoesuv.infomadiun"
        minSdkVersion project.minApiLevel
        targetSdkVersion project.targetApiLevel
        versionCode project.versionCode.toInteger()
        versionName project.versionName
        vectorDrawables.useSupportLibrary true
        setProperty("archivesBaseName", applicationId+"-v"+versionCode+"("+versionName+")")
        resValue "string", "MAPS_API_KEY", apiKeyProperties['MAPS_API_KEY']
        resValue "string", "DIRECTION_API_KEY", apiKeyProperties['DIRECTION_API_KEY']
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
    }
    sourceSets {
        main {
            res.srcDirs = [
                    'src/main/res',
                    'src/main/res-menu',
                    'src/main/res-menu/listplace',
                    'src/main/res-menu/gallery',
                    'src/main/res-menu/maps',
                    'src/main/res-menu/other'
            ]
        }
    }
    buildFeatures {
        dataBinding = true
    }
    lintOptions {
        checkReleaseBuilds = false
        abortOnError false
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

androidExtensions {
    experimental = true
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'junit:junit:4.13'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.1.0'

    implementation "com.google.firebase:firebase-core:$project.firebaseCoreVersion"
    implementation "com.google.android.gms:play-services-maps:$project.firebaseVersion"
    implementation "com.google.android.gms:play-services-location:$project.firebaseVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"

    implementation "com.squareup.retrofit2:retrofit:$project.retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$project.retrofitVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$project.httpLoggingVersion"

    implementation "com.github.bumptech.glide:glide:$project.glideVersion"
    kapt "com.github.bumptech.glide:compiler:$project.glideVersion"
    implementation "com.github.chrisbanes:PhotoView:$project.photoViewVersion"
    implementation "com.github.GrenderG:Toasty:$project.toastyVersion"
    implementation "com.akexorcist:googledirectionlibrary:$project.googleDirectionLibraryVersion"
    implementation "com.intuit.ssp:ssp-android:$project.sspVersion"
    implementation "com.intuit.sdp:sdp-android:$project.sdpVersion"

    implementation "androidx.lifecycle:lifecycle-extensions:$project.lifeCycleVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$project.lifeCycleVersion"
    implementation "androidx.navigation:navigation-fragment-ktx:$navigationVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navigationVersion"

    implementation "android.arch.persistence.room:runtime:$roomVersion"
    kapt "android.arch.persistence.room:compiler:$roomVersion"
}

apply plugin: 'com.google.gms.google-services'
